# Copyleft Mike Arnautov 1984-2003.
#
# And here, at long last, is the main program!
#
#=====================================================================
#
repeat
   local cgi
   local bugfix
   local north.dir
   local f.exit
   local f.count
   local temp.dir
#
   value .of, there
   value .of, here
   call tie.them      # Force initialisation of ties in all circumstances!! 
   ifeq status, -1
      set status, 2
      say restoring.game, arg2
      call restore
   fin
#
# ================== Pre-1.03 incompatibility fix =================
#
   ifloc troll, ylem, limbo, sw.of.chasm
   else
      apport troll, ylem
   fin
   itobj bugfix, schizoid
      ifloc bugfix, ylem
         unflag bugfix, schizoid
      fin
   eoi
   ifeq stage, exits.barred
      and
   ifne door3, closed
      set door3, closed
   fin   
#
# ================== Pre-1.07 incompatibility fix =================
#
   ifflag thurible, seen
   else
      set thurible.spiel, 0
   fin
   ifflag flower, seen
   else
      set stone.flower, 0
   fin
   ifflag anteater, seen
   else
      set anteater.quip, 0
   fin
   ifflag abyss, been.here
   else
      set starry.abyss.1, 0
      set starry.abyss.2, 0
      set distant.glow, 0
      set raging.volcano, 0
   fin
#
# ================== 1.06/07 incompatibility fix ==================
#
   set bugfix, 0
   ifne password, 0
      ifis password, xyzzy
         lda password, snoeze
         set bugfix, 2
      otherwise
      ifis password, plugh
         lda password, snoeze
         set bugfix, -2
      otherwise
      ifis password, noside
         lda password, knerl
         set bugfix, 2
      otherwise
      ifis password, foo
         set bugfix, -2
         lda password, knerl
      otherwise
      ifis password, melenkurion
         lda password, klaetu
         set bugfix, 2
      otherwise
      ifis password, foe
         lda password, klaetu
         set bugfix, -2
      otherwise
      ifis password, fie
         lda password, blerbi
         set bugfix, 2
      else
         set f.count, password
         sub f.count, 2
         ifis f.count, blerbi
            lda password, blerbi
            set bugfix, -2
         fin
      fin
   fin
   ifne bugfix, 0
      lda f.exit, c.exit
      lda north.dir, north
      iterate f.count, 0, 15
         eval temp.dir, f.exit
         add temp.dir, bugfix
         iflt temp.dir, north.dir
            add temp.dir, 8
         else
            sub temp.dir, 8
            iflt temp.dir, north.dir
               add temp.dir, 8
            fin
         fin
         deposit f.exit, temp.dir
         add f.exit, 1
      fin
   fin
#
# Some temporary fixes, only required for beta tests. They override
# "wrong" values saved in testers' games. This whole section can be
# removed once testing is over.
#
   flag pearl, portable
   unflag pearl, schizoid
   flag pearl, object
   unflag well.bottom, in.sewer
   unflag well.bottom, in.desert
   unflag well.bottom, damp
   flag ring, valued
   flag starstone, valued
   iflt strength, carry.limit
      set strength, carry.limit
      add strength, 1      # He must have eaten the mushroom!
   fin
   ifflag island.05, smartass
   else
      flag island.05, hintable   # A hint has been added here.
   fin
   ifflag ridge, smartass
   else
      flag ridge, hintable
   fin
   ifflag abyss, smartass
   else
      flag abyss, hintable
   fin
   flag above.sea, lit
   flag above.sea, no.dwarf
   flag above.sea, not.in.cave
   flag above.sea, no.back
   ifloc rosebowl, limbo
      apport rosebowl, garden.nw
   fin
   apport poster, building
   flag gates, schizoid
   flag door1, schizoid
   flag door2, schizoid
   flag door3, schizoid
   flag trapdoor, schizoid
   flag ladder, schizoid
   apport ladder, storeroom
   apport door1, waterfall
   apport door2, tower.upper.landing
   apport door3, tower.top.landing
   apport trapdoor, shelves.east
   flag door, openable
   flag door1, openable
   flag door2, openable
   flag door3, openable
   flag trapdoor, openable
   flag door, feature
   flag door1, feature
   flag door2, feature
   flag door3, feature
   flag trapdoor, feature
   flag door, seen
   flag door1, seen
   flag door2, seen
   flag door3, seen
   flag trapdoor, seen
   flag rosebowl, unstable
   ifloc rosebowl, garden.nw
   else
      set rosebowl, 1
   fin
   ifeq shoes worn
      and
      not
   ifat caldera, below.ridge
      set shoes, not.worn
   fin
   flag garden.se, h20.here
   ifgt masktime, 5
      and
      not
   ifeq mask, worn
      set masktime, 0
   fin
   ifloc magazines, witts.end
      and
      not
   ifflag witts.end, been.here
      and
      not
   ifeq stage, adventuring
      apport magazines, anteroom
   fin
   itplace place.ptr, ice.cave.1, ice.cave.30
      flag place.ptr, no.dwarf
   fin
#
# ====================== End of beta section ======================
#
   ifeq context q.restored
      say save.the.image
      set context, q.keep.image
   fin
   ifgt stage, little.joke
      or
   ifgt context, none
      proceed
   fin
   ifeq stage, query.restore
      exec 25, cgi            # Are we running in the CGI mode?
      ifgt cgi, 0
         call initialise
      fin
      set context q.initial
      set status, no.match    # Suppress all vocab matching for this loop
      quip game.to.restore
   fin
#
#=====================================================================
#
repeat
   local temp.val
   local event.flag
   local island.event
#
   ifgt context, none
      proceed
   fin
   random   temp.val, 100    # Kick the random number generator
   call is.it.dark?
   ifat island.09
      ifnear anteater
         ifeq anteater, 3
           ifkey look
              and
           ifeq status, 1
           else
              say anteater
           fin
         fin
         ifgt anteater, 2
            apport anteater, ylem
         fin
      fin
      chance 50
         and
      ifloc anteater, limbo
         apport anteater here
         ifflag status, moved
         else
            say anteater
         fin
         flag anteater, seen
      fin
   fin
   ifat above.sea
      ifkey look
         and
      ifeq status, 1
      else
         say blank
      fin
      ifeq above.sea, 0
         smove island, arriving.on.island
      fin
   otherwise
   ifeq rug, hovering
      and
      not
   ifhere rug
      set rug, unrolled.clean
   fin
   ifflag here, in.desert
      call desert.stuff
   fin
   ifflag status, juggled
      and
   ifat cellar
      ifany drop, throw
         call wizard.evicts
      fin
   fin
   chance 4
      and
   ifeq stage, adventuring
      call make.a.dwarf?    # Makes more food too, if necessary
   fin
   ifeq stage, in.repository
      call stop.cheating    # Return fake object to where it belongs
   fin
   ifat east.of.building
      flag seed, seen
   fin
   ifat wrong.venue
      and
   ifeq wrong.venue, 2
      say alternate.venue
      add wrong.venue, 1
   fin
   ifat mists
      ifeq nugget.room, 0
         and
         not
      ifhave gold
         set mists, steps.present
      else
         set mists, steps.gone
      fin
   fin
   ifflag status, moved
      unflag seed, special1
      flag water, moved
      ifnear vampire, thirsty
         flag vampire, special1
      fin
      flag giant, big        # Only matters when he is picnicking
      ifgt giant, picnicking
         and
         not
      ifnear rug
         set giant, picnicking
      fin
      add moves, 1
      ifat mtking
         set snake.exit, there
      fin
      ifinrange here, forest.1, dark.forest
         or
      ifat track, outside.gates
         apport tree, here
      fin
      ifgt dwarf, 0
         ifflag here, not.in.cave
            or
         ifflag here, no.dwarf
            unflag dwarf, big     # Clearly not blocking!
            chance 15
               sub dwarf, 1       # Make dwarves give up gradually
               ifeq dwarf, 0
                  apport dwarf, limbo
               fin
            fin
         else
            ifloc dwarf, here
               or
            ifloc dwarf, there
            else
               set dwarf, 0         # Have shaken the buggars off
               apport dwarf, limbo
            fin
         fin
      fin
      ifflag pirate, special1          # Is he chasing us?
         and
      ifflag here, not.in.cave         # Shake him off
         unflag pirate, special1
      fin
      ifflag there, not.in.cave
         and
         not
      ifflag here, not.in.cave
         unflag pirate, special1       # Fooled him!
      fin
      ifat swirling.mist, stream.maze, plain.2, dark.forest
         call plant.things
      fin
   fin
   ifflag admin, ticker
      call tick
   fin
   ifflag status, moved
      ifnear starstone
         and
      ifgt starstone, quiescent
         ifloc starstone, here
            set starstone, iridescent
         else
            set starstone, darkened
         fin
      fin
      ifflag there, one.exit
         and
      ifloc dwarf, there
         and
         not
      ifflag admin, olorin    # Don't block wizards
         chance 97            # Let him through occasionally
            goto there
            unflag status, moved
            say dwarf.blocks
            flag dwarf, big   # Note the blocking
            proceed
         else
            call sneaks.die
         fin
      fin
   else
      ifnear dwarf
         and
      chance backlash         # Is dwarf particularly vengeful?
         call dwarf.attack
      fin
      proceed
   fin
   unflag status, moved
   lda foof, foof.random
   chance 1
      add foof, 1             # The odd foof
   fin
   ifnear lamp
      ifeq lamp, switched.on
         and
         not
      ifat plain.2            # Don't let it run out in the fog!
         sub lamplife, 1
         ifeq lamplife, 40
            or
         ifeq lamplife, 0
            call lamprey
         fin
      fin
   fin
   ifnear owl
      and
   ifflag status, light.here
      call owl.flies.off
   fin
   ifloc goblins, limbo       # Resting place
      or
   ifloc goblins, ylem        # Gone for good
   else
      apport goblins, here
      ifgt goblins, 0
         ifflag status, light.here
            say goblin.chase
         fin
      fin
   fin
   ifflag here, been.here
      and
   ifflag status, quickie
      or
   ifflag status, fastmode
      or
   ifflag status, brief.now
   else
      flag status, fulldisplay
   fin
   ifflag status, light.here
      say here
      ifat top.of.chapel
         call fishy.stuff
      fin
      ifflag status, show.numbers
         value location.number, here
      fin
      unflag status, brief.now
      ifis there, dark.forest
         and
         not
      ifis here dark.forest
         and
      ifany north, ne, east, se, south, sw, west, nw
         say once.is.enough
      fin
      call special.view
      ifflag here, been.here
         set temp.val, 2
      else
         set temp.val, 3
      fin
      ifflag here, not.in.cave
      else
         sub clock, temp.val           # Tick the clock by 2 or 3
      fin
      flag here, been.here
      iflt dwarf, 1
         unflag dwarf, big             # Absent dwarves don't block
      else
         ifflag here, not.in.cave
            or
         ifflag here, no.dwarf
         else
            apport dwarf, here
         fin
      fin
      call here.you.can.see
      ifnear dwarf
         and
         not
      ifeq swag.to.drop, 0
         and
      chance 67
         ifeq dwarf, 1
            say bulging.coat, 0
         else
            say bulging.coat, 1
         fin
      fin
      ifhave bear
         say bear.follows
      fin
   else
      ifinrange here, above.abyss, imaze.book
         or
      ifat abyss
         ifat above.abyss.2, above.abyss.3, above.abyss.6, above.abyss.7
            or
         ifinrange here, imaze.09, imaze.13
            or
         ifinrange here, imaze.03, imaze.05
            or
         ifat imaze.16
            or
         ifat imaze.02, imaze.03, imaze.06, imaze.07, imaze.08
            say stars.like.dust
         otherwise
         ifat above.glow
            say volcanic.glow
         else
            say it.is.now.dark
         fin
      fin
      ifflag there, lit
         or 
      ifnear chasm, 1
      else
         ifloc lamp, there
            and
         ifeq lamp, switched.on
         else
            ifloc starstone, there
               and
               not
            ifeq starstone, quiescent
            else
               ifhave pyramid
                  or
               chance 25
                  and
                  not
               ifflag admin, ranout
                  and
               iflt stage, in.repository
                  ifnear chasm
                     and
                  ifgt chasm, 0
                  else
                     ifflag here, not.in.cave
                        say crunch, 0
                     else
                        say crunch, 1
                        call coroner
                     fin
                  fin
               fin
            fin
         fin
      fin
      say it.is.now.dark
   fin
   unflag status, fulldisplay
   unflag admin, ranout      # Clear "lamp just died"
#
# Is it cameo time??
#
   chance 1        # Cameos are rare even in the 25%
      and          # of the games in which they can occur
   chance 5
      and
   iflt stage, in.repository
      and
   ifgt turns, 250
      and
      not
   ifflag  here, lit
      call do.cameo
   fin
#
# Litter police!
#
   call litter.check
#
# Warn him about thirst in desert?
#
   ifinrange here, scree, scree.3
      and
   ifflag status, desert.warn
      and
      not
   ifflag quips, desert.rat
      unflag status, desert.warn
      call find.water
      ifeq vessel, none
         ifhave bucket
            say use.bucket, 0
         else
            say use.bucket, 1
         fin
      otherwise
      ifis vessel, flask
         say use.bucket, 1
      otherwise
      ifis vessel, bucket
         and
      ifeq bucket, quarter.full
         or
      ifis vessel, bottle
         say use.bucket, 2
      otherwise
      ifis vessel, bucket
         ifgt bucket, full.of.water
            say use.bucket, 3
         fin
         flag quips, desert.rat
      fin
   fin
#
# Warn him off seeing the imp!
#
   ifat river.north, cave.9
      and
   ifflag here, special
      unflag here, special
      ifat river.north
         say no.gain.there.1
      else
         say no.gain.there.2
      fin
   fin
#
# Warn him to explore more outdoors.
#
   ifgt cave.entered, 0
      set cave.entered, 0
      ifflag notice, seen
         and
      ifflag feeder, seen
      else
         ifflag notice, seen
            or
         ifflag feeder, seen
            say explore.more.outdoors
         else
            say explore.outdoors
         fin
      fin
   fin   
#
# Invoke the hollow voice.
#
   ifat  y2
      chance 35
         say hollow.voice
      fin
   fin
#
# Check for random island events.
#
   ifinrange here, island.se, island.n
      chance 3
         ifflag quips, guybrush.quip
            quip three.heads
            unflag quips, guybrush.quip
         else
            choose island.event, first.island.event, last.island.event
            set event.flag, island.event
            lda temp.val, first.island.event
            sub event.flag, temp.val
            ifflag island.quips, event.flag
            else
               flag island.quips, event.flag
               say blank
               say island.event
            fin
         fin
      fin
   fin
#
# Is he chased by goblins?
#
   call goblin.check
#
# Other events...
#
   ifflag here, not.in.cave
      or
   ifgt stage, exits.barred
      call mask.stuff
   else
      iflt clock, 1
         call events
      else
         unflag status, ticked
         sub swag.time, 1
         ifeq swag.time, 0
            choose swag.time, 10, 20
            call get.swag
         else
            call mask.stuff
         fin
      fin
   fin
#
# Check for attacking dwarves.
#
   ifnear dwarf
      set fleetfoot, 50      # Added 1 to dwarf in the events routine
      call dwarf.attack
   fin
#
# Has he lost the lamp?
#
   ifloc lamp, ylem
      and
   ifat  road
      and
   iflt stage, middle.of.nowhere   # Lamp vanishes during end-game
      say call.it.a.day
      call finis
   fin
#
# Is it time to meet a dwarf willy-nilly?
#
   ifflag axe, seen
      or
      not
   ifflag status, light.here
      or
   iflt moves, 150
      or
      not
   ifflag spider, seen
   else
      call dwarf.encounter, 0
   fin
#
# Is it time to meet the pirate willy-nilly?
#
   ifflag pirate, seen
      or
      not
   ifflag status, light.here
      or
   iflt moves, 200
   else
      call pirate.encounter, 0
   fin
#
#=====================================================================
#
repeat
   local same.hint.loc
   local fangs.disposal
#
   ifgt context, none
      proceed
   fin
   ifgt alarm, 1
      call alarm.stuff
   fin
   ifflag here, hintable
      or
   ifnear unicorn
      ifeq last.hintable, 0
         set last.hintable, here
         set hint.time, 0
      fin
      ifeq last.hintable, here
         add hint.time, 1
         ifgt hint.time, 10
            call hint.logic
         fin
      else
         set same.hint.loc, 0
         ifinrange here, mazea.42, mazea.by.pit
            and
         ifinrange last.hintable, mazea.42, mazea.by.pit
            set same.hint.loc, 1
         otherwise
         ifinrange here, mazed.107, mazed.140
            and
         ifinrange last.hintable, mazed.107, mazed.140
            set same.hint.loc, 1
         otherwise
         ifinrange here, ice.cave.1, ice.cave.30
            and
         ifinrange last.hintable, ice.cave.1, ice.cave.30
            set same.hint.loc, 1
         otherwise
         ifinrange here, catacombs.portal, catacombs.exit
            and
         ifinrange last.hintable, catacombs.portal, catacombs.exit
            set same.hint.loc, 1
         otherwise
         ifflag here, in.desert
            and
         ifinrange last.hintable, desert.1, desert.last
            set same.hint.loc, 1
         fin
         ifeq same.hint.loc, 0
            set hint.time, 0
            set last.hintable, here
         fin
      fin
   fin
   ifnear rats
      add rats, 1
      say rats.here
      ifgt rats, 5
         goto sea.view
         ifhave food
            apport food, limbo
         fin
         set rats, 0
         call coroner
      fin
   fin
   ifflag here, in.sewer
      call prod.tide
   fin
   sub giant.time, 1
   ifnear giant
      call giant.stuff, 0
   otherwise
   ifeq giant, watching
      set giant, picnicking
   fin
   ifgt giant, resting
      and
   iflt giant, picnicking
      and
   iflt giant.time, 1
      ifeq giant, blissful
         set giant, hurt
         unflag giant, seen
         choose giant.time, 4, 7
      else
         ifflag giant, seen
            set giant, resting
         fin
      fin
   fin
   call mutter.stuff
   ifat pantry
      and
      not
   ifflag mouse, seen
      and
   chance 3
      call all.quiet?
      ifeq result, 0
         say tiny.mouse
         flag mouse, seen
      fin
   fin
   ifat dungeon
      call dungeon.stuff
   fin
   ifflag status, juggled        # Did we pick up/drop/move anything?
      unflag status, juggled
      set invct, 0
      itobj obj.ptr
         ifhave obj.ptr
            and
            not
         ifflag obj.ptr, freebie
            add invct, 1
         fin
      eoi
   fin
   call thirst.factor
   set fleetfoot, 25                # Not running, didn't just attack
   set backlash, 35                 # Dwarves not vengeful
   ifflag chalice, special1         # Is chalice full?
      ifflag chalice, special2
         unflag chalice, special2   # Clear the 'just filled' flag
      else
         flag here, damp
         unflag chalice, special1   # Empty it
         say blank
         say chalice.now.empty, 1   # Say it is now empty
      fin
   fin
   ifflag shadow, special2          # This rigmarole with two bits is
      ifnear shadow                 # required to avoid a variety of
         say figure.waves           # unpleasant problems with clearing
      fin                           # arg1 on errors
      unflag shadow, special2
   fin
   ifflag shadow, special1
      flag shadow, special2
      unflag shadow, special1
   fin
   ifflag admin, edison
      and
      not
   ifkey noside
      unflag admin, edison
   fin
#
   iflt turns, max.game
   else
      say wizard.ends
      call finis
   fin
   unflag crown, special1
   ifat breathtaker, faces, warmjunctn
      or
   iflt crown, activated
      or
   ifeq status, 0
      or
   ifflag status, pls.clarify
   else
      ifhave crown
         ifgt crown, activated
            lda text.ptr, voices
            add text.ptr, crown
            sub text.ptr, activated
            say text.ptr
         fin
         iflt crown, speaking
         else
            flag orc.mutter, keys.mutter
         fin
         flag status, ticked
         chance 40
            or
         ifgt crown, speaking
            or
         ifeq crown, activated
            and
            not
         ifeq crown, incomprehensible
            or
         ifkey listen
            add crown, 1
            ifgt crown, killing
               call coroner
            fin
            flag crown, special1
         fin
      else
         ifgt crown dormant
            iflt crown, speaking
            else
               say voices
            fin
            set crown, dormant
         fin
      fin
   fin
   ifflag status, ticked
   else
      ifgt fangs, not.worn
         sub fangs, 1
         ifeq fangs, not.worn
            iflt invct, strength
               set fangs.disposal, 0
            else
               set fangs.disposal, 1
            fin
            say fangs.removed, fangs.disposal
         fin
      fin
   fin
   ifat nowhere
      and
   ifeq turns, 5
      say stuck.in.nowhere
   fin
   ifnear vampire, thirsty
      and
   ifflag vampire, special1
      and
   chance 20
      and
      not
   ifat limbo
      and
   ifflag status, light.here
      and
      not
   ifkey give
      unflag vampire, special1
      say vampire.still.thirsty
   fin
   ifat temple
      and
   ifeq temple, vault.magicked
      say blank
      call temple.magic, 1
   fin
#
#=====================================================================
#
# Done with own actions - get player's command and if there is a
# special context, process the command within that context.
#
repeat
   local context.value
#
   ifeq context, none
      ifnear door1
         and
      ifeq waterfall, opened
         and
      ifeq dwarven, 0
         say blank
         call close.mine.door
      fin
      iflt stage, adventuring
         set status, no.amatch   # No approximate matching during the "joke"
      fin
      input
      proceed
   fin
   input null
   ifeq context, q.input
      set context, none
   fin
   ifeq context, none
      proceed
   fin
#
# Got some pending context that needs handling.
#
   ifeq context, q.help
      or
   ifeq context, q.help.cost
      call hint.action
   otherwise
#
   ifeq context, q.spire.drop
      call do.spire.drop
   otherwise
#
   ifeq context, q.whirlpool
      call whirlpool.dive
   otherwise
#
   ifeq context, q.quit
      ifeq status, 0
         or
      ifany yes, quit
         call finis
      fin
      ifany quit, no, n
         set context, none
         quip ok
      fin
      ifkey maybe
         set context, none
         quip indecisions
      fin
      say yes.or.no?
      quip want.to.quit?
   otherwise
#
   ifeq context, q.wizard
      or
   ifeq context, q.prove.it
      call positive.vetting
   otherwise
#
   ifeq context, q.forest.out
      set context, none
      ifeq status, 0
         or
      ifkey yes
         add penalties, hint.cost
         smove road, foof
      fin
      respond quit, no, n, ok
      respond maybe, indecisions
      set context, q.forest.out
      say yes.or.no?
      quip want.out?
   otherwise
#
   ifeq context, q.maze.out
      call maze.escape
   otherwise
#
   ifeq context, q.kill.giant
      set context, none
      ifeq status, 0
         or
      ifkey yes
         quip very.funny
      fin
      ifany quit, no, n
         say use.a.weapon, arg1
         quip kill.the.whatever, arg2
      fin
      quip lets.be.prudent, 0
   otherwise
#
   ifeq context, q.save.what
      ifeq status, 0
         set context, none
         quip u.turn
      fin
      ifeq status, 1
         exec 11
         call save.this
      fin
   otherwise
#
   ifeq context, q.delete
      ifeq status, 0
         or
      ifkey yes
         exec 24, text.ptr
         exec 3
         call save.this
      fin
      set context, none
      respond no, n, as.you.wish
      quip lets.be.prudent, 0
   otherwise
#
   ifeq context, q.save
      or
   ifeq context, q.continue
      call save.it
   otherwise
#
   ifeq context, q.remember
      or
   ifeq context, q.recall
      call art.of.memory
   otherwise
#
   ifeq context, q.more
      ifkey maybe
         set status, 0
      fin
      ifeq status, 0
         or
      ifkey yes
         call keep.talking, talk.next, talk.end
      fin
      ifany quit, no, n
         set context, none
         ifflag here, been.here
            say ok
         fin
         quit
      fin
      say yes.or.no?
      quip more?
   otherwise
#
   ifeq context, q.intro
      set stage, adventuring
      ifkey maybe
         set status, 0
         say lets.be.prudent, 1
         say blank
      fin
      ifeq status, 0
         or
      ifkey yes
         set context, none
         call do.introduction
         quip blank
      fin
      ifany quit, no, n
         set context, none
         ifflag context, joke.over
            say as.i.was.saying
         fin
         quit
      fin
      say yes.or.no?
      quip instructions?
   otherwise
#
   ifeq context, q.forbidding
      set context, none
      ifkey yes
         quip moebius.gates
      fin
      respond no, n, just.as.well
      quip lets.be.prudent, 0
   otherwise
#
   ifeq context q.chalice
      ifkey yes
         flag admin, quest.accepted
         apport unicorn, here
         flag unicorn, seen
         set context, none
         quip gosh.unicorn!
      fin
      ifany quit, no, n
         set context, none
         quip hollow.laugh
      fin
      say yes.or.no?
      quip chalice.query, 1
   otherwise
#
   ifeq context, q.suicide
      set context, none
      ifeq status, 0
         or
      ifkey yes
         flag hints, death.is.simple
         quip walk.without.light
      fin
      respond quit, no, n, things.could.be.worse
      respond maybe, lets.be.prudent, 0
      set context, q.suicide
      say yes.or.no?
      quip really.suicide?
   otherwise
#
   ifeq context, q.oyster
      set context, none
      ifeq status, 0
         or
      ifkey yes
         flag oyster, big     # Mark as read
         add penalties, hint.cost
         quip oyster.memo
      fin
      respond quit, no, n, as.you.wish
      respond maybe, indecisions
      set context, q.oyster
      say yes.or.no?
      quip read.oyster?
   otherwise
#
   ifeq context, q.reincarnate
      call resurrection.shuffle
   otherwise
#
   ifeq context, q.dragon
      call dragon.slayer
   otherwise
#
   ifeq context, q.dwarf
      call dwarf.slayer
   otherwise
#
   ifeq context, q.ogre
      call ogre.slayer
   otherwise
#
   ifeq context, q.sentry
      set context, none
      ifkey yes
         call sentry.kills
      fin
      respond quit, no, n, just.as.well
      quip lets.be.prudent, 0
   otherwise
#
   ifeq context, q.cheat
      ifeq status, 0
         or
      ifkey yes
         set context, none
         add penalties, hint.cost
         itplace place.ptr, mazea.42, mazea.by.pit
            deposit place.ptr, 1
         eoi
         quip you.are.now.cheating
      fin
      ifany quit, no, n
         set context, none
         quip i.am.glad
      fin
      say yes.or.no?
      quip really.cheat?
   otherwise
#
   ifeq context, q.ledge.bad
      or
   ifeq context, q.ledge.good
      set context.value, context
      set context, none
      ifkey yes
         ifeq context.value, q.ledge.bad
            say ledge.too.narrow
            goto beanstalk.bottom
            call coroner
         else
            unflag here, hintable
            smove ledge, sunstone.helps
         fin
      fin
      respond quit, no, n, need.sunstone
      quip lets.be.prudent, 0
   otherwise
#
   ifeq context, q.vocab.1
      call show.nouns
   otherwise
#
   ifeq context, q.vocab.0
      or
   ifeq context, q.vocab.2
      or
   ifeq context, q.vocab.3
      ifany quit, no, n
         set context, none
         quip as.you.wish
      fin
      ifeq context, q.vocab.0
         set context, q.vocab.1
         say vocab.verbs
         quip more?
      otherwise
      ifeq context, q.vocab.2
         set context q.vocab.3
         say vocab.travel
         quip more?
      else
         say vocab.screen
      fin
      set context, none
      quit
   otherwise
#
   ifeq context, q.good.news.1
      or
   ifeq context, q.good.news.2
      ifeq status, 0
         or
      ifkey yes
      else
         set context.value, q.good.news.2
         sub context.value, context
         say have.it.anyway, context.value
      fin
      ifeq context, q.good.news.1
         set context, q.good.news.2
         say treasure.2
         quip more.good.news?
      fin
      ifgt scorex, 768
         say treasure.4
      else
         say treasure.3
      fin
      call finis                 # End the game in triumph
   otherwise
#
   ifeq context, q.bad.news
      ifeq status, 0
         or
      ifkey yes
      else
         say too.late
      fin
      say cave.2.destroyed
      stop
   otherwise
#
   ifeq context, q.bad.image
      set context, none
      ifeq status, 0
         or
      ifkey yes
         exec 24
         exec 3                 # Delete the save file
      otherwise
      ifany no, n
      else
         quip lets.be.prudent, 0
      fin
      quip ok
   otherwise
#
   ifeq context, q.keep.image
      or
   ifeq context, q.restored
      call restored
   otherwise
#
   ifeq context q.early.restore
      call early.restore
   otherwise
#
   ifeq context, q.restore.what
      set context, none
      ifeq status, 0
         quip u.turn
      fin
      exec 11
      call restore
   otherwise
#
   ifeq context, q.initial
      ifeq status, 0
         call initialise
      else
         exec 11
         call restore
      fin
   otherwise
#
   ifeq context, q.waterfall
      set context, none
      ifeq status, 0
         or
      ifkey yes
         smove waterfall, through.waterfall
      fin
      ifany no, n
      else
         quip lets.be.prudent, 0
      fin
      quip if.not.not
   fin
#
   ifeq context, q.really.fly?
      set context, none
      ifkey yes
         flag admin, ok.to.fly
         call fly
      fin
      ifany no, n
      else
         quip lets.be.prudent, 0
      fin
      quip if.not.not
   otherwise
#
   ifeq context, q.into.abyss?
      set context, none
      ifeq status, 0
         or
      ifkey yes
         smove above.abyss, over.abyss
      fin
      respond n, no, if.not.not
      quip lets.be.prudent, 0
   fin
#
   ifeq context, q.streambed
      set context, none
      ifkey yes
         say on.streambed
         goto ylem
         call coroner
      fin
      ifany n, no
         quip very.sensible
      fin
      quip lets.be.prudent, 0
   fin
#
#=====================================================================
#
# No special context. Proceed at will.
#
repeat
   local the.it
   local typo.check
   local stone.check
   local in.loop
   local temp.val
#
   ifeq status, badsyntax
      flush
      ifinrange arg1, first.magic, last.magic
         or
      ifkey say
         and
      ifinrange arg2, first.magic, last.magic
         quip one.at.a.time
      fin
      respond again, no.understand
      quip bad.syntax
   fin
#
# Rude words override everything else! (Well, nearly.)
#
   ifat west.pit
      or
   ifat beanstalk.bottom
      respond fuck, no.piss.plant
   else
      respond fuck, no.fucking
   fin
#
# We generally ignore "room"
#
   ifkey room
      ifeq status, 1
         ifflag here, not.in.cave
            and
            not
         ifflag here, indoors
            quip room.enough, 2
         otherwise
         ifflag here, small
            quip room.enough, 0
         else
            quip room.enough, 1
         fin
      fin
      ifis arg1, room
         exec 29           # Swap arg1 and arg2.
      fin
      set arg2, none
      set status, 1
   fin
#
# No digging, please!
#
   respond dig, need.shovel
#
# Handle 'it'.
#
   ifgt it, 0
      ifis arg1, it
         or
      ifis arg2, it
         set the.it, it
         fakecom it, the.it
      fin
   fin
   ifeq status, 2
      set it, arg2
   otherwise
   ifflag arg1, object
      or
   ifinrange arg1, first.pseudo, last.pseudo
      set it, arg1
   fin
#
# Handle "around"
#
   ifkey around
      ifkey look
         ifis arg1, around
            exec 29
         fin
         set status, 1
      otherwise
      ifkey walk
         quip how.to.go.around
      else
         quip no.understand
      fin
   fin
#
# Check for an obvious dummy
#
   ifeq status, 1
      and
   ifkey walk
      call walk
   fin
#
# Let's be kind. R is REPEAT (or AGAIN), but players type R BIRD
# meaning RELEASE BIRD (as per adv550/660). So the kernel does not
# intercept REPEAT if R <ANYTHING> gets typed. We handle it here instead.
#
   ifkey again
      fakecom again, drop
   fin
#
# Now for the rest of it...
#
   respond search, cant.search
#
# Can we see anything?
#
   ifkey look
      ifkey fog
         and
      ifinrange here, plain.1, plain.3
         quip pea.souper
      fin
      ifkey darkness
         ifinrange here, above.abyss.2, above.abyss.7
            or
         ifinrange here, imaze.01, imaze.17
            ifhave lamp, switched.on
               quip darkness.too.dark, 1
            else
               quip darkness.too.dark, 0
            fin
         fin
         ifflag here, lit
            quip pardon?
         otherwise
         ifflag status, light.here
            quip darkness.too.dark, 1
         else
            quip darkness.too.dark, 0
         fin
      otherwise
      ifflag status, light.here
      else
         chance 5
            quip its.too.dark
         fin
         quip no.light.here
      fin
   fin
#
# Handle liquid.
#
   ifkey liquid
      ifinrange here, phoney.shaft, sewer.5
         fakecom liquid, sewage
      otherwise
      ifflag here, h20.here
         or
      ifinrange here, island, island.ne
         or
      ifinrange here, rocky.beach, beach.end
         or
      ifat below.ridge, caldera
         fakecom liquid, water
      otherwise
      ifat east.pit
         fakecom liquid, oil
      otherwise
      ifnear vial
         fakecom liquid, vial
      else
         call find.water
         ifne vessel, none
            fakecom liquid, water
         otherwise
         ifnear bucket, full.of.champagne
            fakecom liquid, champagne
         otherwise
         ifnear bottle, full.of.oil
            or
         ifnear flask, full.of.oil
            fakecom liquid, oil
         fin
      fin
   fin      
#
# Handle all teeth references.
#
   ifkey tooth
      call tooth.stuff
   fin
#
# Handle sun(s)/sunstone
#
   ifkey sunstone
      and
   ifat basalt.shelf
      and
      not
   ifnear sunstone
      and
      not
   ifany up, find, fetch
      fakearg sunstone, sun
   fin
#
# Handle star(s)/starstone
#
   ifkey stars
      and
   ifflag starstone, seen
      ifinrange here, above.abyss.2, above.abyss.7
         or
      ifinrange here, imaze.01, imaze.13
         or
      ifat imaze.16
         and
         not
      ifany drop, throw
      else
         ifnear rod
            and
            not
         ifnear starstone
         else
            fakearg star, starstone
         fin
      fin
   fin
   ifkey star
      ifnear rod
         or
      ifat ne.repository
         ifnear rod
            respond look, rusty.star, 0
         else
            respond look, rusty.star, 1
         fin
         respond get, remove, break, value, no.point.in.that
         respond rub, nothing
         ifgt status, 1
            quip hah!
         fin
      fin
   fin
   ifkey starstone
      and
      not
   ifhave starstone
      and
   ifinrange here, above.abyss.2, above.abyss.7
      or
   ifinrange here, imaze.01, imaze.13
      or
   ifat imaze.16
      fakecom starstone, stars
   fin
#
# Handle fragments
#
   ifkey fragments
      and
   ifnear shards
      fakearg fragments, shards
   fin   
#
# Handle stone (sun or star)
#
   ifkey stone
      ifnear sunstone
         and
      ifnear starstone
         set stone.check, 0
         ifhave sunstone
             set stone.check, 1
         fin
         ifhave starstone
             add stone.check, 1
         fin
         ifeq stone.check, 1
            ifkey get
               ifhere sunstone
                  fakecom stone, sunstone
               else
                  fakecom stone, starstone
               fin
            otherwise
            ifany drop, throw
               ifhave sunstone
                  fakecom stone, sunstone
               else
                  fakecom stone, starstone
               fin
            fin
         else
            quip which.stone?
         fin
      otherwise
      ifnear sunstone
         fakecom stone, sunstone
      otherwise
      ifnear starstone
         fakecom stone, starstone
      fin
   fin
#
# Arsonists not wanted!
#
   ifkey light, fire
      quip no.fires
   fin
#
# Handle roses.
#
   ifkey rose
      ifinrange here, garden.n, garden.se
         fakearg rose, roses
      otherwise
      ifnear windrose
         fakecom rose, windrose
      else
         quip i.dont.see, roses
      fin
   fin
#
#
# Handle packet/wrapper.
#
   ifkey paper
      and
   ifnear packet
      fakecom paper, packet
   otherwise
   ifkey packet
      and
   ifnear paper
      ifany open, pour, empty
         quip torn.packet
      fin
      fakecom packet, paper
   fin
#
# Handle mud/mudshoes
#
   ifkey mud
      and 
   ifnear shoes
      fakecom mud, shoes
   fin
#
# Handle tube (as in lava tube) and tube (as in rolled rug)
#
   ifkey tube
      and
   ifnear rug, rolled.up
      fakecom tube, rug
   fin
#
# Equate steps and stairs, except at mists.
#
   ifkey stairs
      and
      not
   ifat mists
      fakearg stairs, steps
   fin
#
# Pretend there is a combination lock on the chest.
#
   ifis arg2, lock
      ifat depression, in.cave
         or
      ifnear chest
         ifany open, break, look, hit
            ifat depression, in.cave
               quip its.a.lock, 0
            otherwise
            ifnear chest
               quip its.a.lock, 1
            fin
         otherwise
         ifis arg1, close
         else
            quip hah!
         fin
      else
         respond look, i.dont.see, arg2
      fin
   fin
#
# Shut the mine door, if speaking English.
#
   ifnear door1
      and
   ifeq waterfall, opened
      and
   ifeq dwarven, 0
      call close.mine.door
      say blank
   fin
#
# Make sure "dynamite" is not revealed by "get all"!
#
   ifeq stage, in.repository
      set dynamite, 0
      ifkey dynamite
         exec 26, in.loop      # Check for a "do all" loop.
         ifeq in.loop, 0       # Not in a loop.
            set dynamite, 1    # He actually said "dynamite"!
         fin
      fin
   fin
#
# Intercept a reference to the label on the box of Turkish delight.
#
   ifkey label
      and
   ifnear box
      fakecom label, box
   fin
#
# Handle non-specific get/drop cases.
#
   ifeq status, 1
      ifkey get
         default here, portable
      otherwise
      ifkey drop
         default inhand
      fin
   otherwise
   ifkey all
      ifge arg3, badword
         call shadow.shutup
         ifeq arg3, badsyntax
            quip no.except, arg3
         fin
         ifeq arg3, ambigword
            quip tell.me.more, arg3
         fin
         ifeq arg3, badword
            flush
            quip nocomprende.object, arg3
         fin
         ifeq arg3, ambigtypo
            quip is.it.a.typo? arg3
         fin
      fin
      ifany drop, down
         call except.check
         call drop.check, 0
         doall inhand
      otherwise
      ifkey get
         ifat east.of.building
            and
            not
         ifnear seed
            apport seed, here
         fin
         ifflag status, light.here
            call except.check
            doall here, portable
         else
            quip cant.see.anything, 1
         fin
      fin
   fin
#
# Handle treasure.
#
   ifkey treasure
      ifany drop, down
         call except.check
         call drop.check, 1
         doall inhand, valued
      otherwise
      ifkey get
         call except.check
         doall here, valued
      else
         quip one.at.a.time
      fin
   fin
#
# Handle "follow me"
#
   ifkey follow, me
      quip no.follow.you
   fin
#
#  Threepwood quips
#
   ifany guybrush, monkey
      respond monkey, no.monkeys
      quip wrong.game
   fin
#
# A few house-keeping oddments...
#
   sub foobar, 1
   add turns, 1
   ifeq turns, 20
      unflag status, vocab.prod
   fin
   call olorin.check
#
# Hmmm... Don't recall why we need to check for it here.
# Safer to leave it. (Might be a remnant from before the time
# when I introduced the range of verbs not requiring noun-checking.)
#
   ifkey save
      call save
      quit
   fin
#
# Ignore null commands.
#
   ifeq status, 0
      quit
   fin
#
# Verb not understood. Try to explain.
#
   ifeq arg1, badword
      or
   ifeq arg1, ambigword
      or
   ifeq arg1, ambigtypo
      ifeq arg1, badword
         flush
         ifgt status, 1
            chance 33
               say nocomprende, arg1
            else
               say nocomprende.verb, arg1
            fin
         else
            say nocomprende, arg1
         fin
         ifflag status, vocab.prod
            say use.vocabulary
            unflag status, vocab.prod
         fin
      otherwise
      ifeq arg1, ambigword
         say tell.me.more, arg1
      else
         say is.it.a.typo?, arg1
         ifflag status, vocab.prod
            say use.vocabulary
            unflag status, vocab.prod
         fin
      fin
      call shadow.shutup
      quit
   fin
#
# Saying magic words is special (and handled via 'actions').
#
   ifkey say
      ifinrange arg2, first.magic, last.magic
      else
         call say
      fin
   fin
#
# Handle "all".
#
   ifkey all
      and
      not
   ifkey say
      call check.all
   fin
#
# If noun not understood, try to respond appropriately.
#
   ifgt status, 1
      ifeq arg2, badword
         or
      ifeq arg2, ambigword
         or
      ifeq arg2, ambigtypo
         or
      ifeq arg2, sceneword
         ifinrange arg1, first.special, last.special
         else
            ifeq arg2, ambigword
               say tell.me.more, arg2
            otherwise
            ifeq arg2, badword
               flush
               say nocomprende.object, arg2
               ifflag status, vocab.prod
                  say use.vocabulary
                  unflag status, vocab.prod
               fin
            otherwise
            ifeq arg2, sceneword
               and
            ifgt stage, little.joke
               quip just.scenery, arg2
            else
               say is.it.a.typo?, arg2
               ifflag status, vocab.prod
                  say use.vocabulary
                  unflag status, vocab.prod
               fin
            fin
            call shadow.shutup
            quit
         fin
      fin
   fin
#
# Initialise some default values for this round.
#
   set drop.here, here       # Default location for dropping things
   set throw.here, here      # Default location for throwing things
   set drop.text, none
   set throw.text, none
   set throw.it, false
   set abort, false          # Safety net
   set vessel, none          # Ditto
   set silence, false        # Ditto
#
# Do some command faking...
#
# To players, beanstalks are plants and v.v.
#
   ifkey beanstalk
      ifnear plant
         or
      ifnear plant2          # Fudge the plant to be a beanstalk!
         fakearg beanstalk, plant
      fin
   fin
   ifkey plant
      ifnear beanstalk
         or 
      ifnear beanstalk2    # Fudge beanstalk to be a plant!
         fakearg plant, beanstalk
      fin
   fin
#
# Both obolus and drachma are coins, OK?
#
   ifkey coins
      ifnear drachma
         fakecom coins, drachma
      otherwise
      ifnear obolus
         fakecom coins, obolus
      fin
   fin
#
# A seal might be referred to as "something" (but so can the alarm!)
#
   ifkey something
      and
   ifnear seal
      and
      not
   ifat top.of.chapel
      fakearg something, seal
   fin
#
# A thurible might be mistaken for a rattle.
#
   ifkey rattle
      and 
   ifnear, thurible
      iflt not.a.rattle, 3
         say not.a.rattle
      fin
      ifeq not.a.rattle, 1
         quit
      fin 
      fakecom rattle, thurible
   fin
#
# Generally gold means the nugget, but it may mean the crucifix.
#
   ifkey gold
      and
   ifnear crucifix
      and
      not
   ifnear nugget
      fakecom gold, crucifix
   fin
#
# Fiddle doorway to mean doors, if near doors.
#
   ifkey doorway
      ifnear door
         or
      ifnear door1
         or
      ifnear door2
         or
      ifnear door3
         fakecom doorway, door
      fin
   fin
#
# Fiddle the spire in the desert being referred to as a column.
#
   ifkey column
      ifat basalt.shelf
         and
      ifinrange basalt.shelf, 1, 2
         or
      ifinrange here, desert.12, desert.last
         or
      ifinrange here, north.of.spire, spire
         or
      ifat basalt.shelf.2
         fakearg, column, spire
      fin
   fin
#
# Remind hime that he need not keep saying 'go'.
#
   ifkey west, walk
      add west.count, 1
      ifeq west.count, 5
         ifis arg1, walk
            say west.will.do, arg1
         else
            say west.will.do, arg2
         fin
      fin
   fin
   ifeq stage, mirror.world
      call swap.directions
   fin
#
# The sculpture changes it's shape and hence name.
#
   ifgt status, 1
      and
   ifinrange arg2, first.sculpture.fake, last.sculpture.fake
      call sculpture.stuff
   fin
#
# Trap generic references to catacombs.
#
   ifkey catacombs
      and
   ifany go, in, fly
      ifinrange here, catacombs.portal, catacombs.exit
         quip you.are.there
      otherwise
      ifkey fly
         and
      ifflag admin, olorin
         move catacombs.13
      otherwise
      ifat catacombs.lobby, rest.area, audience, audience.east
         quip enter.catacombs
      else
         quip dont.know.the.way
      fin
   fin
#
# Travel by magic words is special, but otherwise we may need
# to check the sunstone!
#
   ifkey say          # Must avoid shout jump and suchlike at window
      and             # and other places; we do this by only doing
   ifgt status, 1     # at processing if verb is not say or the word
      and             # said is magic
      not
   ifinrange arg2, first.magic, last.magic
   else
      set direction, 0
      ifat ledge
         and
      ifkey back
         fakearg back, south
      fin
      ifflag here, not.in.cave
         and
         not
      ifflag here, indoors
         and
      ifhave sunstone
         and
         not
      ifkey look
         ifinrange arg1, first.direction, last.compass.point
            set direction, arg1
         else
            ifgt status, 1
               and
            ifinrange arg2, first.direction, last.compass.point
               set direction, arg2
            fin
         fin
         ifgt direction, 0
            call consult.sunstone, direction
         fin
      fin
#
# Cater for a variety of foresty things.
#
      ifinrange here, forest.1, path
         call in.forest.actions
      fin
#
# Finally handle all place-specific stuff.
#
      call here
   fin
#
# Handle null jumps.
#
   ifkey jump
      and
   ifeq status, 2
      ifany off, across, down
      else
         quip you.jump
      fin
   fin      
#
# Fiddle the directions, if necessary
#
   ifeq stage, mirror.world
      call swap.directions
   fin
#
# Handle references to place names
#
   ifflag arg1, place
      ifat  arg1
         quip you.are.there
      else
         quip dont.know.the.way
      fin
   else
#
# Some objects are, yet are not present in the repository.
#
      ifeq stage, in.repository
         call start.cheating    # Kludge up dummy objects in repository
         ifany rod, bundle
            call rod.special.stuff
         fin
         respond bundle, plurals, pile, no.plural.please
      fin
# 
# Curtains do not really exists, nor do bundles (except in the repsitory).
#
      ifkey curtains
         ifat soft, curtain.entrance, bare.cavern
            apport curtains, here
            flag curtains, seen
         fin
      fin
      respond bundle, plurals, pardon?
#
# Some other minor quipperies...
#
      respond left, forward, need.a.direction
      ifkey debris
         call debris.quip
         quit
      fin
#
# Do we mean Orion or O'Ryan?
#
      ifkey orion
         ifflag quips, oryan.quip
            quip orion.quip, 1
         fin
         quip orion.quip, 0
      fin
      ifkey oryan
         ifeq temple, vault.locked
            quip pat.oryan, 0
         fin
         quip pat.oryan,1
      fin
#
# Handle map references
#
      ifkey map
         ifeq status, 1
            or
         ifany get, find, read, look
            quip no.map
         fin
      fin
#
# Handle references to the illusory Zorro outfit.
#
      ifany hat, cloak
         ifflag mask, seen
            ifkey hat
               lda obj.ptr, hat
            else
               lda obj.ptr, cloak
            fin
            ifhave mask, worn
               quip just.an.illusion, obj.ptr
            fin
            quip i.dont.see, obj.ptr
         else
            quip pardon?
         fin
      fin
#
# Some special responses for the sewers.
#
      ifflag here, in.sewer
         or
      ifat sea.view     
         ifkey sewage
            ifkey get
               and
            ifflag beanstalk, seen
               quip sewage.h20.bad, 1
            otherwise
            ifkey look
               quip no.look.sewage
            else
               describe dung
               quit
            fin
         fin
         ifkey get, water
            or
         ifkey fill
            ifflag beanstalk, seen
               quip sewage.h20.bad, 1
            else
               quip sewage.h20.bad, 0
            fin
         fin
      fin
      call arg1
#
# Only come here if verb fails to deal
#
      call shadow.shutup     
      ifflag arg1, object
         ifkey water
            and
         ifflag here, h20.here
            or
         ifnear arg1
            say do.what?, arg1
            flag status, pls.clarify   # Request clarification
         else
            flush
            ifflag arg1, seen
              say i.dont.see, arg1
            else
               say pardon?
            fin
         fin
      else
         call bail.out
      fin
   fin
   quit
#
#=====================================================================
#
repeat      # Dummy. Just stops translator grizzling.
   respond furiously, crabs, boils, quark, stew, pardon?
#
#=====================================================================

